

/*
 * Copyright (c) Terl Tech Ltd • 09/05/18 18:38 • goterl.com
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v2.0. If a copy of the MPL was not distributed with this
 * file, you can obtain one at http://mozilla.org/MPL/2.0/.
 */

plugins {
    id 'com.android.library'
    id 'signing'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.3"
}

ext {
    artifactId = "lazysodium-android"
    groupId = "com.goterl.lazycode"
    version = '3.4.2'
    description = "Lazysodium (Android) makes it effortless for Android " +
            "developers to get started with Libsodium's cryptography."
    androidTestDir = "src/androidTest/java/com/goterl/lazycode/lazysodium"
    javaTestDir = "../lazysodium-java/src/test/java"
}

group project.ext.groupId
version = project.ext.version

android {
    compileSdkVersion 28
    buildToolsVersion "27.0.3"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 13
        versionName project.ext.version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDirs = ['src/main/jniLibs']

        java {
            srcDir "../lazysodium-java/src/main/java"
            // Be sure to update lint.xml in the project root if any other
            // excluded files are added.
            exclude "com/goterl/lazycode/lazysodium/utils/NativeUtils.java"
            exclude "com/goterl/lazycode/lazysodium/SodiumJava.java"
            exclude "com/goterl/lazycode/lazysodium/LazySodiumJava.java"
        }
    }

    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }

    packagingOptions {
        exclude '../lazysodium-java/src/test/java'
    }
}

tasks.withType(Javadoc).all { enabled = false }

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}


// You can add extra file names to
// exclude here
def excludeTheseFilesFromTests = ['BaseTest.java']

// Exclude every file from LazySodiumJava
// that is not a file and also does not contain "Android"
ext.getExcludedTestFileNames = { ->
    def exclude = fileTree(javaTestDir).filter { it.isFile() && !it.name.contains("Android") }.files.name
    return exclude + excludeTheseFilesFromTests
}

// Get the test files EXCLUDING the
// filenames that get aggregated from the above function
ext.getTestFiles = { ->
    def excludeTheseFiles = getExcludedTestFileNames()
    def files = fileTree(javaTestDir).exclude(excludeTheseFiles)
    return files
}


// This task will try to delete all the files
// that were copied from the 'copyTests' tasks
task deleteCopiedTests(type: Task) {
    doFirst {
        def files = getTestFiles()
        files.each { file ->
            def f = new File(androidTestDir + "/" + file.name)
            if (f.exists()) {
                println "Deleted: " + f.getName()
                delete f
            }
        }
    }
}

// Copy all the tests from the LazysodiumJava
// project and put them into this project.
// We overwrite if there is already a file here.
task copyTests(type: Copy) {
    def names = getExcludedTestFileNames()
    from javaTestDir
    into androidTestDir
    include "*"
    exclude names
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

// This task adds the string "package com.goterl.lazycode.lazysodium;"
// to the start of every test. This depends on "copyTests"
task addPackageStatements(type: Task, dependsOn: "copyTests") {
    doLast {
        def files = getTestFiles()
        files.each { file ->
            // If the file exists in LazysodiumAndroid (this project)
            // then prepend the line
            def f = new File(androidTestDir + "/" + file.name)
            if (f.exists()) {
                f.write("package com.goterl.lazycode.lazysodium;\n" + f.text)
            }
        }
    }
}

// Everytime we build, please perform "addPackageStatements"
preBuild.dependsOn copyTests
preBuild.dependsOn addPackageStatements


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'junit:junit:4.12'
    api 'net.java.dev.jna:jna:4.5.2@aar'
}

// Tasks
signing {
    // This will use the GPG Agent
    // to sign the jar
  useGpgCmd()
  sign configurations.archives
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


task signPom(type: Sign) {
    sign project.file("$buildDir/publications/mavenJava/pom-default.xml")

    // The signing plugin does not seem to notice
    // it when the publications folder with the
    // signature has been deleted. So we always
    // create a new signature
    outputs.upToDateWhen { false }
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.ext.groupId
            artifactId project.ext.artifactId
            version project.ext.version

            artifact bundleRelease
            artifact sourcesJar
            artifact javadocJar


            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.ext.description)
                root.appendNode('name', project.ext.artifactId)
                root.appendNode('url', 'https://github.com/terl/lazysodium-android')

                def scm = root.appendNode('scm')
                scm.appendNode("connection", "scm:git:git://github.com/terl/lazysodium-android.git")
                scm.appendNode("developerConnection", "scm:git:ssh://github.com/terl/lazysodium-android")
                scm.appendNode("url", "http://github.com/terl/lazysodium-android")

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'Mozilla Public License, Version 2.0')
                license.appendNode('url', 'http://www.mozilla.org/MPL/2.0/index.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Terl Tech Ltd')
                developer.appendNode('email', 'hello@terl.co')

                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if(it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


bintray {
    user = findProperty("bintray.user")
    key = findProperty("bintray.apiKey")
    publications = ['mavenJava']
    publish = true
    configurations = ['archives']
    pkg {
        repo = 'lazysodium-maven'
        name = project.ext.artifactId
        userOrg = 'terl'
        desc = project.ext.description
        licenses = ['MPL-2.0']
        issueTrackerUrl = "https://github.com/terl/lazysodium-android/issues"
        vcsUrl = 'https://github.com/terl/lazysodium-android.git'

        version {
            name = project.ext.version
            released = new java.util.Date()
            vcsTag = project.ext.version
        }
    }
}


model {
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.withType(Sign)
    }
}
